plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.7.5"
}

group = "com.ontraport.sdk"
def travis_build = System.getenv("TRAVIS_BUILD_NUMBER") ?: "0"
version = "0.10." + travis_build

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.2"

    testImplementation group: "junit", name: "junit", version: "4.12"
    testImplementation "org.mockito:mockito-core:2.19.0"
}

publishing {
    publications {
        artifactPlugin(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = System.getenv("ARTIFACTORY_URL")
    publish {
        repository {
            repoKey = 'libs'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
        }
        defaults {
            println "Building artifact version " + version
            publications 'artifactPlugin'
            properties = ['Deployed By': System.properties['user.name']]
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
            publishIvy = false
        }
    }

    clientConfig.includeEnvVars = true
    clientConfig.envVarsExcludePatterns = '*pwd*,*password*,*PWD*,*PASSWORD*,*Password,*secret*,*SECRET*,*key*,*KEY*,sonar.login'
    clientConfig.info.buildName = name
    clientConfig.info.buildNumber = version
}
